class Nanosatellite;
class Battery;
class PayloadState;
class Window;
class SolarPanel;
class Memory;
class STX;
class C3D;
class TOPCAT;
class JANUS;
class MIC;
class FUNCUBE;
class UVTRX;
class ConstantlyActivePayload;
class C3DPowerConsumption;

class Nanosatellite 
{
	Battery mainBattery;
	Memory memory;
	SolarPanel panel;
	STX stx;
	C3D c3d;
	TOPCAT topcat;
	JANUS janus;
	MIC mic;
	FUNCUBE funcube2;
	UVTRX uvtrx;
	ConstantlyActivePayload constantpayloads;
		    
	Nanosatellite(Battery b, Memory m)
	{
    	mainBattery = b;
    	memory = m;
        panel = new SolarPanel(this);
        stx = new STX(this);
        c3d = new C3D(this);
        topcat = new TOPCAT(this);
        janus = new JANUS(this);
        mic = new MIC(this);
        funcube2 = new FUNCUBE(this);
        uvtrx = new UVTRX(this);
        constantpayloads = new ConstantlyActivePayload(this, INSERTconstantlyActiveBatteryConsumption);
  	}
}

class C3D extends Timeline
{
    Nanosatellite nanosat;
    PayloadState state;
    C3DPowerConsumption power;
    Window camWindow;
    Window rmWindow;
    
    C3D(Nanosatellite n)
    {
        nanosat = n;
        state = new PayloadState();
        power = new C3DPowerConsumption(this);
        camWindow = new Window();
        rmWindow = new Window();
    }
    
    action turnOnC3D {
	    eq(INSERTdurationC3DturnOn, duration);
	}
	
	action turnOffC3D {
	    eq(INSERTdurationC3DturnOff, duration);
	}
    
    action takePhoto {
	    eq(INSERTdurationPhoto, duration);
	}
	
	action runHouseKeepingModeBeforeCamera {
	    eq(INSERTdurationC3Dhk, duration);
	}
	
	action runHouseKeepingModeBeforeRM {
	    eq(INSERTdurationC3Dhk, duration);
	}
	
	action measureRadiation {
	    eq(INSERTdurationRadiation, duration);
	}
}

class C3DPowerConsumption
{
    C3D c3d;

    C3DPowerConsumption(C3D c)
    {
        c3d = c;
    }

    action consumePowerWhileOnC3D {
	    eq(1, duration);
	}
}

class TOPCAT
{
    Nanosatellite nanosat;
    PayloadState state;
    
    TOPCAT(Nanosatellite n)
    {
        nanosat = n;
        state = new PayloadState();
    }
    
    action turnOnTOPCAT{
	    eq(INSERTdurationTOPCATturnOn, duration);
	}
	
	action turnOffTOPCAT{
	    eq(INSERTdurationTOPCATturnOff, duration);
	}
	
	action consumePowerWhileOnTOPCAT {
	    eq(1, duration);
	}
	
	action runExperimentTOPCAT {
	    eq(INSERTdurationTOPCAT, duration);
	}
}

class JANUS extends Timeline
{
    Nanosatellite nanosat;
    Window window;
    PayloadState state;
    
    JANUS(Nanosatellite n)
    {
        nanosat = n;
        window = new Window();
        state = new PayloadState();
    }
    
    action runExperimentLowPowerJANUS {
        eq(INSERTdurationJANUSlow, duration);
    }
    
    action runExperimentHighPowerJANUS {
        eq(INSERTdurationJANUShigh, duration);
    }
    
    action consumePowerWhileRunningLowJANUS {
        eq(1, duration);
    }
    
    action consumePowerWhileRunningHighJANUS {
        eq(1, duration);
    }
}

class MIC
{
    Nanosatellite nanosat;
    PayloadState state;
    
    MIC(Nanosatellite n)
    {
        nanosat = n;
        state = new PayloadState();
    }
    
    action turnOnMIC{
	    eq(INSERTdurationMICturnOn, duration);
	}
	
	action turnOffMIC{
	    eq(INSERTdurationMICturnOff, duration);
	}
	
	action consumePowerWhileOnMIC {
	    eq(1, duration);
	}
	
	action runExperimentMIC {
	    eq(INSERTdurationMIC, duration);
	}
}

class ConstantlyActivePayload
{
    Nanosatellite nanosat;
    float consumptionRate;
    
    ConstantlyActivePayload(Nanosatellite n, float r)
    {
        nanosat = n;
        consumptionRate = r;
    }
    
    action alwaysOn {
        eq(1, duration);
    }
}

class FUNCUBE extends Timeline
{
    Nanosatellite nanosat;
    Window educationWindow;
    Window amateurWindow;
    
    FUNCUBE(Nanosatellite n)
    {
        nanosat = n;
        educationWindow = new Window();
        amateurWindow = new Window();
    }
    
    action educationalCommunicationsFUNCUBE {
        eq(1, duration);
    }
    
    action amateurCommunicationsFUNCUBE {
        eq(1, duration);
    }
}

class Battery extends Reservoir
{
    string profileType;
    //string detectorType;
    // initial capacity, min, max, max production rate, max production overall, max consumption rate, max consumption overall
	Battery(float ic, float min, float max)
	{
		super(ic, min, max);
		profileType="IncrementalFlowProfile";
		//profileType = "GroundedProfile";
        //detectorType = "GroundedFVDetector";
	}
}

class Memory extends Reservoir
{
    string profileType;
    
    // ic = initial capacity, min, max
	Memory(float ic, float min, float max)
	{
		super(ic, min, max);
		profileType="IncrementalFlowProfile";
	}
}

// Time window for availability
class Window extends Timeline
{
    predicate Available {}
}

class PayloadState extends Timeline
{
	predicate On {}
	predicate Off {}
	predicate poweringUp {}
	predicate poweringDown {}	
}

class SolarPanel
{
    Nanosatellite nanosat;
    Window window;
	
	SolarPanel(Nanosatellite n)
	{
		nanosat = n;
		window = new Window();
	}
	
	action chargeBattery {
	    eq(1, duration);
	}
}

class STX
{
    Nanosatellite nanosat;
    Window window;
    
    STX(Nanosatellite n)
    {
        nanosat = n;
        window = new Window();
    }
    
    action downlinkData{
        eq(1, duration);
    }
}


class UVTRX extends Timeline
{
    Nanosatellite nanosat;
    Window beaconWindow;
    Window downlinkWindow;
    
    UVTRX(Nanosatellite n)
    {
        nanosat = n;
        downlinkWindow = new Window();
        beaconWindow = new Window();
    }
    
    action downlinkData{
        eq(1, duration);
    }
    
    action beaconSignal{
        eq(1, duration);
    }
}

// Actions

C3DPowerConsumption::consumePowerWhileOnC3D
{
    contained_by(condition object.c3d.state.On);
    starts(effect object.c3d.nanosat.mainBattery.consume p);
    eq(p.quantity, INSERTC3DbatteryConsumption);
    bool continueLoopC;
    if(continueLoopC) {
        meets(effect object.consumePowerWhileOnC3D next);
        next.end <= INSERThorizon;
        starts(effect object.c3d.nanosat.mainBattery.consume p);
        eq(p.quantity, INSERTC3DbatteryConsumption);
    }    
}

C3D::turnOnC3D
{
    contained_by(condition object.nanosat.mic.state.Off);
    met_by(condition object.state.Off);
	equals(effect object.state.poweringUp);
	meets(effect object.state.On);
	meets(condition object.runHouseKeepingModeBeforeRM);
}

C3D::turnOffC3D
{
	met_by(condition object.state.On);
	equals(effect object.state.poweringDown);
	meets(effect object.state.Off);
}

C3D::takePhoto
{
    starts_during(condition object.nanosat.uvtrx.downlinkWindow.Available);
    contained_by(condition object.camWindow.Available);
    contained_by(condition object.state.On);
    met_by(object.runHouseKeepingModeBeforeCamera);
    ends(effect object.nanosat.memory.produce data);
    eq(data.quantity, INSERTC3DdataGeneratedCamera);
}

C3D::runHouseKeepingModeBeforeCamera
{
    contained_by(condition object.state.On);
    meets(object.takePhoto);
    ends(effect object.nanosat.memory.produce data);
    eq(data.quantity, INSERTC3DdataGeneratedHouseKeeping);
    starts(effect object.nanosat.mainBattery.consume power);
    eq(power.quantity, INSERTC3DbatteryConsumption);
}

C3D::measureRadiation
{
    starts_during(condition object.rmWindow.Available);
    contained_by(condition object.state.On);
    met_by(object.runHouseKeepingModeBeforeRM);
    ends(effect object.nanosat.memory.produce data);
    eq(data.quantity, INSERTC3DdataGeneratedRM);
}

C3D::runHouseKeepingModeBeforeRM
{
    contained_by(condition object.state.On);
    meets(object.measureRadiation);
    ends(effect object.nanosat.memory.produce data);
    eq(data.quantity, INSERTC3DdataGeneratedHouseKeeping);
    starts(effect object.nanosat.mainBattery.consume power);
    eq(power.quantity, INSERTC3DbatteryConsumption);
}

TOPCAT::turnOnTOPCAT
{
	met_by(condition object.state.Off);
	equals(effect object.state.poweringUp);
	meets(effect object.state.On);
}

TOPCAT::turnOffTOPCAT
{
	met_by(condition object.state.On);
	equals(effect object.state.poweringDown);
	meets(effect object.state.Off);
}

TOPCAT::consumePowerWhileOnTOPCAT
{
    contained_by(object.state.On);
    starts(effect object.nanosat.mainBattery.consume power);
    eq(power.quantity, INSERTTOPCATbatteryConsumption);
    bool continueLoopT;
    if(continueLoopT) {
        meets(effect object.consumePowerWhileOnTOPCAT next);
        next.end <= INSERThorizon;
        starts(effect object.nanosat.mainBattery.consume power);
        eq(power.quantity, INSERTTOPCATbatteryConsumption);
    }
}

TOPCAT::runExperimentTOPCAT
{
    met_by(object.turnOnTOPCAT);
    meets(object.turnOffTOPCAT);
    contained_by(condition object.state.On);
    ends(effect object.nanosat.memory.produce data);
    eq(data.quantity, INSERTTOPCATdataGenerated);
}

JANUS::runExperimentLowPowerJANUS
{
    ends(effect object.nanosat.memory.produce data);
    eq(data.quantity, INSERTJANUSdataGenerated);
}

JANUS::consumePowerWhileRunningLowJANUS
{
    contained_by(object.runExperimentLowPowerJANUS);
    starts(effect object.nanosat.mainBattery.consume power);
    eq(power.quantity, INSERTJANUSbatteryConsumptionLow);
    bool continueLoopJL;
    if(continueLoopJL) {
        meets(effect object.consumePowerWhileRunningLowJANUS next);
        next.end <= INSERThorizon;
        starts(effect object.nanosat.mainBattery.consume power);
        eq(power.quantity, INSERTJANUSbatteryConsumptionLow);
    }
}

JANUS::runExperimentHighPowerJANUS
{
    contained_by(condition object.nanosat.topcat.state.Off);
    contained_by(condition object.nanosat.c3d.state.Off);
    contained_by(condition object.nanosat.mic.state.Off);
    starts_during(condition object.window.Available);
    ends(effect object.nanosat.memory.produce data);
    eq(data.quantity, INSERTJANUSdataGenerated);
}

JANUS::consumePowerWhileRunningHighJANUS
{
    contained_by(object.runExperimentHighPowerJANUS);
    starts(effect object.nanosat.mainBattery.consume power);
    eq(power.quantity, INSERTJANUSbatteryConsumptionHigh);
    starts_during(object.window.Available);
    bool continueLoopJH;
    if(continueLoopJH) {
        meets(effect object.consumePowerWhileRunningHighJANUS next);
        next.end <= INSERThorizon;
        starts(effect object.nanosat.mainBattery.consume power);
        eq(power.quantity, INSERTJANUSbatteryConsumptionHigh);
    }
}

MIC::turnOnMIC
{
    contained_by(condition object.nanosat.c3d.state.Off);
	met_by(condition object.state.Off);
	equals(effect object.state.poweringUp);
	meets(effect object.state.On);
}

MIC::turnOffMIC
{
	met_by(condition object.state.On);
	equals(effect object.state.poweringDown);
	meets(effect object.state.Off);
}

MIC::consumePowerWhileOnMIC
{
    contained_by(object.state.On);
    starts(effect object.nanosat.mainBattery.consume power);
    eq(power.quantity, INSERTMICbatteryConsumption);
    bool continueLoopM;
    if(continueLoopM) {
        meets(effect object.consumePowerWhileOnMIC next);
        next.end <= INSERThorizon;
        starts(effect object.nanosat.mainBattery.consume power);
        eq(power.quantity, INSERTMICbatteryConsumption);
    }
}

MIC::runExperimentMIC
{
    contained_by(condition object.nanosat.c3d.state.Off);
    contained_by(condition object.nanosat.topcat.state.Off);
    met_by(object.turnOnMIC);
    meets(object.turnOffMIC);
    contained_by(condition object.state.On);
    ends(effect object.nanosat.memory.produce data);
    eq(data.quantity, INSERTMICdataGenerated);
}

FUNCUBE::educationalCommunicationsFUNCUBE
{
    contained_by(condition object.educationWindow.Available);
    starts(effect object.nanosat.mainBattery.consume power);
    eq(power.quantity, INSERTFUNCUBEbatteryConsumptionEdu);
    bool continueLoopFE;
    if(continueLoopFE) {
        meets(effect object.educationalCommunicationsFUNCUBE next);
        next.end <= INSERThorizon;
        starts(effect object.nanosat.mainBattery.consume power);
        eq(power.quantity, INSERTFUNCUBEbatteryConsumptionEdu);
    }
}

FUNCUBE::amateurCommunicationsFUNCUBE
{
    contained_by(condition object.amateurWindow.Available);
    starts(effect object.nanosat.mainBattery.consume power);
    eq(power.quantity, INSERTFUNCUBEbatteryConsumptionAma);
    bool continueLoopFA;
    if(continueLoopFA) {
        meets(effect object.amateurCommunicationsFUNCUBE next);
        next.end <= INSERThorizon;
        starts(effect object.nanosat.mainBattery.consume power);
        eq(power.quantity, INSERTFUNCUBEbatteryConsumptionAma);
    }
}

ConstantlyActivePayload::alwaysOn
{
    bool continueLoopCAP;
    starts(effect object.nanosat.mainBattery.consume power);
    eq(power.quantity, object.consumptionRate);
    if(continueLoopCAP) {
        meets(effect object.alwaysOn next);
        next.end <= INSERThorizon;
        starts(effect object.nanosat.mainBattery.consume power);
        eq(power.quantity, object.consumptionRate);
    }
}

SolarPanel::chargeBattery
{
    contained_by(condition object.window.Available);
  	bool continueLoopB;
    if(continueLoopB) {
        meets(effect object.chargeBattery next);
        next.end <= INSERThorizon;
        starts(effect object.nanosat.mainBattery.produce power);
        eq(power.quantity, [0,INSERTbatteryChargingRate]);
    }
}

STX::downlinkData
{
    contained_by(condition object.window.Available);
    starts(effect object.nanosat.mainBattery.consume consumption);
    eq(consumption.quantity, INSERTSTXbatteryConsumption);
    bool continueLoopS;
    if(continueLoopS) {
        meets(effect object.downlinkData next);
        next.end <= INSERThorizon;
        starts(effect object.nanosat.memory.consume data);
        eq(data.quantity, [0,INSERTSTXdownlinkRate]);
        starts(effect object.nanosat.mainBattery.consume consumption);
        eq(consumption.quantity, INSERTSTXbatteryConsumption);
    }
}

UVTRX::downlinkData
{
    contained_by(condition object.downlinkWindow.Available);
    starts(effect object.nanosat.mainBattery.consume consumption);
    eq(consumption.quantity, INSERTUVTRXbatteryConsumptionDownlink);
    bool continueLoopUD;
    if(continueLoopUD) {
        meets(effect object.downlinkData next);
        next.end <= INSERThorizon;
        starts(effect object.nanosat.memory.consume data);
        eq(data.quantity, [0,INSERTUVTRXdownlinkRate]);
        starts(effect object.nanosat.mainBattery.consume consumption);
        eq(consumption.quantity, INSERTUVTRXbatteryConsumptionDownlink);
    }
}

UVTRX::beaconSignal
{
    contained_by(condition object.beaconWindow.Available);
    starts(effect object.nanosat.mainBattery.consume consumption);
    eq(consumption.quantity, INSERTUVTRXbatteryConsumptionBeacon);
    bool continueLoopUB;
    if(continueLoopUB) {
        meets(effect object.beaconSignal next);
        next.end <= INSERThorizon;
        starts(effect object.nanosat.mainBattery.consume consumption);
        eq(consumption.quantity, INSERTUVTRXbatteryConsumptionBeacon);
    }
}