class Nanosatellite;
class Battery;
class Camera;
class PayloadState;
class Window;
class SolarPanel;
class Memory;
class Transmitter;
class C3D;
class RadiationMonitor;

class Nanosatellite 
{
	Battery mainBattery;
	Memory memory;
	C3D c3d;
	SolarPanel panel;
	Transmitter transmitter;
		    
	Nanosatellite(Battery b, Memory m)
	{
    	mainBattery = b;
    	memory = m;
        c3d = new C3D(this);
        panel = new SolarPanel(this);
        transmitter = new Transmitter(this);
  	}
}

class C3D
{
    Nanosatellite nanosat;
    Camera camera;
    RadiationMonitor rm;
    PayloadState state;
    Window window;
    
    C3D(Nanosatellite n)
    {
        nanosat = n;
        camera = new Camera(this);
        rm = new RadiationMonitor(this);
        state = new PayloadState();
        window = new Window();
    }
    
    action turnOn{
	    eq(1, duration);
	}
	
	action turnOff{
	    eq(1, duration);
	}
	
	action consumePowerWhileOn {
	    eq(1, duration);
	}
	
	action runHouseKeepingModeBeforeCamera {
	    eq(1, duration);
	}
	
	action takePhoto {
	    eq(30, duration);
	}
	
	action runHouseKeepingModeBeforeRM {
	    eq(1, duration);
	}
	
	action measureRadiation {
	    eq(40, duration);
	}
}

// The battery

class Battery extends Reservoir
{
    string profileType;
    
    // ic = initial capacity, min, max
	Battery(float ic, float min, float max)
	{
		super(ic, min, max);
		profileType="IncrementalFlowProfile";
	}
}

// The memory

class Memory extends Reservoir
{
    string profileType;
    
    // ic = initial capacity, min, max
	Memory(float ic, float min, float max)
	{
		super(ic, min, max);
		profileType="IncrementalFlowProfile";
	}
}

// Time window for availability

class Window extends Timeline
{
    predicate Available {}
}

class PayloadState extends Timeline
{
	predicate On {}
	predicate Off {}
	predicate poweringUp {}
	predicate poweringDown {}	
}

class Camera
{
    C3D c3d;
	PayloadState state;
	Window window;
	
	Camera(C3D c)
	{
		c3d = c;
	    window = new Window();
	}
		
	action takePhoto{
    	eq(30, duration);
	}
}

class RadiationMonitor
{
    C3D c3d;
	PayloadState state;
	Window window;
	
	RadiationMonitor(C3D c)
	{
		c3d = c;
	    window = new Window();
	}
		
	action measureRadiation{
    	eq(40, duration);
	}
}

class SolarPanel
{
    Nanosatellite nanosat;
	Window window;
	
	SolarPanel(Nanosatellite n)
	{
		nanosat = n;
	    window = new Window();
	}
	
	action chargeBattery {
	    eq(1, duration);
	}
}

class Transmitter
{
    Nanosatellite nanosat;
    Window window;
    
    Transmitter(Nanosatellite n)
    {
        nanosat = n;
        window = new Window();
    }
    
    action downlinkData{
        eq(1, duration);
    }
}

// Actions

C3D::takePhoto
{
    equals(object.camera.takePhoto);
    met_by(object.runHouseKeepingModeBeforeCamera);
    contained_by(condition object.state.On);
    starts(effect object.nanosat.memory.produce data);
    eq(data.quantity, 691200);
}

Camera::takePhoto
{
    contained_by(condition object.window.Available);
}

C3D::measureRadiation
{
    equals(object.rm.measureRadiation);
    met_by(object.runHouseKeepingModeBeforeRM);
    contained_by(condition object.state.On);
    starts(effect object.nanosat.memory.produce data);
    eq(data.quantity, 18432);
}

RadiationMonitor::measureRadiation
{
    contained_by(condition object.window.Available);
}

C3D::turnOn
{
    before(object.takePhoto);
    contained_by(condition object.window.Available);
	met_by(condition object.state.Off);
	equals(effect object.state.poweringUp);
	meets(effect object.state.On);
}

C3D::turnOff
{
    contained_by(condition object.window.Available);
	met_by(condition object.state.On);
	equals(effect object.state.poweringDown);
	meets(effect object.state.Off);
}

C3D::consumePowerWhileOn
{
    contained_by(condition object.window.Available);
    contained_by(object.state.On);
    starts(effect object.nanosat.mainBattery.consume power);
    mulEq(duration, 13, power.quantity);
    bool continueLoopC;
    if(continueLoopC) {
        meets(effect object.consumePowerWhileOn next);
        next.end <= 1500;
        starts(effect object.nanosat.mainBattery.consume p);
        mulEq(duration, 13, p.quantity);
    }
}

C3D::runHouseKeepingModeBeforeCamera
{
    meets(object.takePhoto);
    contained_by(condition object.state.On);
    starts(effect object.nanosat.memory.produce data);
    eq(data.quantity, 256);
}

C3D::runHouseKeepingModeBeforeRM
{
    meets(object.measureRadiation);
    contained_by(condition object.state.On);
    starts(effect object.nanosat.memory.produce data);
    eq(data.quantity, 256);
}

SolarPanel::chargeBattery
{
    contained_by(condition object.window.Available);
  	bool continueLoopB;
    if(continueLoopB) {
        meets(effect object.chargeBattery next);
        next.end <= 1500;
        starts(effect object.nanosat.mainBattery.produce power);
        mulEq(duration, 133, power.quantity);
    }
}

Transmitter::downlinkData
{
    contained_by(condition object.window.Available);
    bool continueLoop;
    if(continueLoop == true) {
        meets(effect object.downlinkData next);
        next.end <= 1500;
        starts(effect object.nanosat.memory.consume data);
        mulEq(duration, 72000, data.quantity);
        starts(effect object.nanosat.mainBattery.consume consumption);
        mulEq(duration, 453, consumption.quantity);
    }
}