class Nanosatellite;
class Battery;
class PayloadState;
class Window;
class SolarPanel;
class Memory;
class STX;
class C3D;
class TOPCAT;
class JANUS;
class MIC;
class FUNCUBE;
class UVTRX;
class ConstantlyActivePayload;
class C3DPowerConsumption;

class Nanosatellite 
{
	Battery mainBattery;
	Memory memory;
	SolarPanel panel;
	STX stx;
	C3D c3d;
	TOPCAT topcat;
	JANUS janus;
	MIC mic;
	FUNCUBE funcube2;
	UVTRX uvtrx;
	ConstantlyActivePayload constantpayloads;
		    
	Nanosatellite(Battery b, Memory m)
	{
    	mainBattery = b;
    	memory = m;
        panel = new SolarPanel(this);
        stx = new STX(this);
        c3d = new C3D(this);
        topcat = new TOPCAT(this);
        janus = new JANUS(this);
        mic = new MIC(this);
        funcube2 = new FUNCUBE(this);
        uvtrx = new UVTRX(this);
        constantpayloads = new ConstantlyActivePayload(this, 53.4);
  	}
}

class C3D extends Timeline
{
    Nanosatellite nanosat;
    PayloadState state;
    C3DPowerConsumption power;
    Window camWindow;
    Window rmWindow;
    
    C3D(Nanosatellite n)
    {
        nanosat = n;
        state = new PayloadState();
        power = new C3DPowerConsumption(this);
        camWindow = new Window();
        rmWindow = new Window();
    }
    
    action turnOnC3D {
	    eq(10, duration);
	}
	
	action turnOffC3D {
	    eq(30, duration);
	}
    
    action takePhoto {
	    eq(1800, duration);
	}
	
	action runHouseKeepingModeBeforeCamera {
	    eq(1, duration);
	}
	
	action runHouseKeepingModeBeforeRM {
	    eq(1, duration);
	}
	
	action measureRadiation {
	    eq(2400, duration);
	}
}

class C3DPowerConsumption
{
    C3D c3d;

    C3DPowerConsumption(C3D c)
    {
        c3d = c;
    }

    action consumePowerWhileOnC3D {
	    eq(1, duration);
	}
}

class TOPCAT
{
    Nanosatellite nanosat;
    PayloadState state;
    
    TOPCAT(Nanosatellite n)
    {
        nanosat = n;
        state = new PayloadState();
    }
    
    action turnOnTOPCAT{
	    eq(900, duration);
	}
	
	action turnOffTOPCAT{
	    eq(30, duration);
	}
	
	action consumePowerWhileOnTOPCAT {
	    eq(1, duration);
	}
	
	action runExperimentTOPCAT {
	    eq(2400, duration);
	}
}

class JANUS extends Timeline
{
    Nanosatellite nanosat;
    Window window;
    PayloadState state;
    
    JANUS(Nanosatellite n)
    {
        nanosat = n;
        window = new Window();
        state = new PayloadState();
    }
    
    action runExperimentLowPowerJANUS {
        eq(2100, duration);
    }
    
    action runExperimentHighPowerJANUS {
        eq(1200, duration);
    }
    
    action consumePowerWhileRunningLowJANUS {
        eq(1, duration);
    }
    
    action consumePowerWhileRunningHighJANUS {
        eq(1, duration);
    }
}

class MIC
{
    Nanosatellite nanosat;
    PayloadState state;
    
    MIC(Nanosatellite n)
    {
        nanosat = n;
        state = new PayloadState();
    }
    
    action turnOnMIC{
	    eq(30, duration);
	}
	
	action turnOffMIC{
	    eq(30, duration);
	}
	
	action consumePowerWhileOnMIC {
	    eq(1, duration);
	}
	
	action runExperimentMIC {
	    eq(900, duration);
	}
}

class ConstantlyActivePayload
{
    Nanosatellite nanosat;
    float consumptionRate;
    
    ConstantlyActivePayload(Nanosatellite n, float r)
    {
        nanosat = n;
        consumptionRate = r;
    }
    
    action alwaysOn {
        eq(1, duration);
    }
}

class FUNCUBE extends Timeline
{
    Nanosatellite nanosat;
    Window educationWindow;
    Window amateurWindow;
    
    FUNCUBE(Nanosatellite n)
    {
        nanosat = n;
        educationWindow = new Window();
        amateurWindow = new Window();
    }
    
    action educationalCommunicationsFUNCUBE {
        eq(1, duration);
    }
    
    action amateurCommunicationsFUNCUBE {
        eq(1, duration);
    }
}

class Battery extends Reservoir
{
    string profileType;
    
    // initial capacity, min, max, max production rate, max production overall, max consumption rate, max consumption overall
	Battery(float ic, float min, float max)
	{
		super(ic, min, max);
		profileType="IncrementalFlowProfile";
	}
}

class Memory extends Reservoir
{
    string profileType;
    
    // ic = initial capacity, min, max
	Memory(float ic, float min, float max)
	{
		super(ic, min, max);
		profileType="IncrementalFlowProfile";
	}
}

// Time window for availability
class Window extends Timeline
{
    predicate Available {}
}

class PayloadState extends Timeline
{
	predicate On {}
	predicate Off {}
	predicate poweringUp {}
	predicate poweringDown {}	
}

class SolarPanel
{
    Nanosatellite nanosat;
    Window window;
	
	SolarPanel(Nanosatellite n)
	{
		nanosat = n;
		window = new Window();
	}
	
	action chargeBattery {
	    eq(1, duration);
	}
}

class STX
{
    Nanosatellite nanosat;
    Window window;
    
    STX(Nanosatellite n)
    {
        nanosat = n;
        window = new Window();
    }
    
    action downlinkData{
        eq(1, duration);
    }
}


class UVTRX extends Timeline
{
    Nanosatellite nanosat;
    Window beaconWindow;
    Window downlinkWindow;
    
    UVTRX(Nanosatellite n)
    {
        nanosat = n;
        downlinkWindow = new Window();
        beaconWindow = new Window();
    }
    
    action downlinkData{
        eq(1, duration);
    }
    
    action beaconSignal{
        eq(1, duration);
    }
}

// Actions

C3DPowerConsumption::consumePowerWhileOnC3D
{
    contained_by(condition object.c3d.state.On);
    bool continueLoopC;
    if(continueLoopC) {
        meets(effect object.consumePowerWhileOnC3D next);
        next.end <= 86400;
        starts(effect object.c3d.nanosat.mainBattery.consume p);
        eq(p.quantity, 900);
    }    
}

C3D::turnOnC3D
{
    contained_by(condition object.nanosat.mic.state.Off);
    met_by(condition object.state.Off);
	equals(effect object.state.poweringUp);
	meets(effect object.state.On);
	meets(condition object.runHouseKeepingModeBeforeRM);
}

C3D::turnOffC3D
{
	met_by(condition object.state.On);
	equals(effect object.state.poweringDown);
	meets(effect object.state.Off);
}

C3D::takePhoto
{
    starts_during(condition object.nanosat.uvtrx.downlinkWindow.Available);
    contained_by(condition object.camWindow.Available);
    contained_by(condition object.state.On);
    met_by(object.runHouseKeepingModeBeforeCamera);
    ends(effect object.nanosat.memory.produce data);
    eq(data.quantity, 691200);
}

C3D::runHouseKeepingModeBeforeCamera
{
    contained_by(condition object.state.On);
    meets(object.takePhoto);
    ends(effect object.nanosat.memory.produce data);
    eq(data.quantity, 256);
    starts(effect object.nanosat.mainBattery.consume power);
    eq(power.quantity, 900);
}

C3D::measureRadiation
{
    starts_during(condition object.rmWindow.Available);
    contained_by(condition object.state.On);
    met_by(object.runHouseKeepingModeBeforeRM);
    ends(effect object.nanosat.memory.produce data);
    eq(data.quantity, 18432);
}

C3D::runHouseKeepingModeBeforeRM
{
    contained_by(condition object.state.On);
    meets(object.measureRadiation);
    ends(effect object.nanosat.memory.produce data);
    eq(data.quantity, 256);
    starts(effect object.nanosat.mainBattery.consume power);
    eq(power.quantity, 900);
}

TOPCAT::turnOnTOPCAT
{
	met_by(condition object.state.Off);
	equals(effect object.state.poweringUp);
	meets(effect object.state.On);
}

TOPCAT::turnOffTOPCAT
{
	met_by(condition object.state.On);
	equals(effect object.state.poweringDown);
	meets(effect object.state.Off);
}

TOPCAT::consumePowerWhileOnTOPCAT
{
    contained_by(object.state.On);
    bool continueLoopT;
    if(continueLoopT) {
        meets(effect object.consumePowerWhileOnTOPCAT next);
        next.end <= 86400;
        starts(effect object.nanosat.mainBattery.consume power);
        eq(power.quantity, 1600);
    }
}

TOPCAT::runExperimentTOPCAT
{
    met_by(object.turnOnTOPCAT);
    meets(object.turnOffTOPCAT);
    contained_by(condition object.state.On);
    ends(effect object.nanosat.memory.produce data);
    eq(data.quantity, 204800);
}

JANUS::runExperimentLowPowerJANUS
{
    ends(effect object.nanosat.memory.produce data);
    eq(data.quantity, 512);
}

JANUS::consumePowerWhileRunningLowJANUS
{
    contained_by(object.runExperimentLowPowerJANUS);
    bool continueLoopJL;
    if(continueLoopJL) {
        meets(effect object.consumePowerWhileRunningLowJANUS next);
        next.end <= 86400;
        starts(effect object.nanosat.mainBattery.consume power);
        eq(power.quantity, 780);
    }
}

JANUS::runExperimentHighPowerJANUS
{
    contained_by(condition object.nanosat.topcat.state.Off);
    contained_by(condition object.nanosat.c3d.state.Off);
    contained_by(condition object.nanosat.mic.state.Off);
    starts_during(condition object.window.Available);
    ends(effect object.nanosat.memory.produce data);
    eq(data.quantity, 512);
}

JANUS::consumePowerWhileRunningHighJANUS
{
    contained_by(object.runExperimentHighPowerJANUS);
    starts_during(object.window.Available);
    bool continueLoopJH;
    if(continueLoopJH) {
        meets(effect object.consumePowerWhileRunningHighJANUS next);
        next.end <= 86400;
        starts(effect object.nanosat.mainBattery.consume power);
        eq(power.quantity, 2400);
    }
}

MIC::turnOnMIC
{
    contained_by(condition object.nanosat.c3d.state.Off);
	met_by(condition object.state.Off);
	equals(effect object.state.poweringUp);
	meets(effect object.state.On);
}

MIC::turnOffMIC
{
	met_by(condition object.state.On);
	equals(effect object.state.poweringDown);
	meets(effect object.state.Off);
}

MIC::consumePowerWhileOnMIC
{
    contained_by(object.state.On);
    bool continueLoopM;
    if(continueLoopM) {
        meets(effect object.consumePowerWhileOnMIC next);
        next.end <= 86400;
        starts(effect object.nanosat.mainBattery.consume power);
        eq(power.quantity, 1000);
    }
}

MIC::runExperimentMIC
{
    contained_by(condition object.nanosat.c3d.state.Off);
    contained_by(condition object.nanosat.topcat.state.Off);
    met_by(object.turnOnMIC);
    meets(object.turnOffMIC);
    contained_by(condition object.state.On);
    ends(effect object.nanosat.memory.produce data);
    eq(data.quantity, 34816);
}

FUNCUBE::educationalCommunicationsFUNCUBE
{
    contained_by(condition object.educationWindow.Available);
    bool continueLoopFE;
    if(continueLoopFE) {
        meets(effect object.educationalCommunicationsFUNCUBE next);
        next.end <= 86400;
        starts(effect object.nanosat.mainBattery.consume power);
        eq(power.quantity, 1014);
    }
}

FUNCUBE::amateurCommunicationsFUNCUBE
{
    contained_by(condition object.amateurWindow.Available);
    bool continueLoopFA;
    if(continueLoopFA) {
        meets(effect object.amateurCommunicationsFUNCUBE next);
        next.end <= 86400;
        starts(effect object.nanosat.mainBattery.consume power);
        eq(power.quantity, 970);
    }
}

ConstantlyActivePayload::alwaysOn
{
    bool continueLoopCAP;
    if(continueLoopCAP) {
        meets(effect object.alwaysOn next);
        next.end <= 86400;
        starts(effect object.nanosat.mainBattery.consume power);
        eq(power.quantity, object.consumptionRate);
    }
}

SolarPanel::chargeBattery
{
    contained_by(condition object.window.Available);
  	bool continueLoopB;
    if(continueLoopB) {
        starts(effect object.nanosat.mainBattery.produce power);
        eq(power.quantity, [0,6500]);
        meets(effect object.chargeBattery next);
        next.end <= 86400;
    }
}

STX::downlinkData
{
    contained_by(condition object.window.Available);
    bool continueLoopS;
    if(continueLoopS) {
        meets(effect object.downlinkData next);
        next.end <= 86400;
        starts(effect object.nanosat.memory.consume data);
        eq(data.quantity, [0,4194304]);
        starts(effect object.nanosat.mainBattery.consume consumption);
        eq(consumption.quantity, 5440);
    }
}

UVTRX::downlinkData
{
    contained_by(condition object.downlinkWindow.Available);
    bool continueLoopUD;
    if(continueLoopUD) {
        meets(effect object.downlinkData next);
        next.end <= 86400;
        starts(effect object.nanosat.memory.consume data);
        eq(data.quantity, [0,72000]);
        starts(effect object.nanosat.mainBattery.consume consumption);
        eq(consumption.quantity, 390);
    }
}

UVTRX::beaconSignal
{
    contained_by(condition object.beaconWindow.Available);
    bool continueLoopUB;
    if(continueLoopUB) {
        meets(effect object.beaconSignal next);
        next.end <= 86400;
        starts(effect object.nanosat.mainBattery.consume consumption);
        eq(consumption.quantity, 295);
    }
}