class Nanosatellite;
class Battery;
class Camera;
class CameraState;
class Window;
class SolarPanel;
class SolarPanelState;
class Memory;
class Transmitter;

class Nanosatellite 
{
	Battery mainBattery;
	Memory memory;
	Camera camera;
	SolarPanel panel;
	Transmitter transmitter;
    
	Nanosatellite(Battery b, Memory m)
	{
    	mainBattery = b;
    	memory = m;
        camera = new Camera(this);
        panel = new SolarPanel(this);
        transmitter = new Transmitter(this);
  	}
  	  	
	action takePhoto {}
	action chargeBattery {}
	action downlinkData {}
}

// The battery

class Battery extends Reservoir
{
    string profileType;
    
    // ic = initial capacity, level limit min, level limit max, production max, consumption max
	Battery(float ic, float lmin, float lmax, float pmax, float pmin)
	{
		super(ic, lmin, lmax, pmax, pmin);
		profileType="IncrementalFlowProfile";
	}
}

// The memory

class Memory extends Reservoir
{
    string profileType;
    
    // ic = initial capacity, level limit min, level limit max, production max, consumption max
	Memory(float ic, float lmin, float lmax, float pmax, float pmin)
	{
		super(ic, lmin, lmax, pmax, pmin);
		profileType="IncrementalFlowProfile";
	}
}

// Time window for availability

class Window extends Timeline
{
    predicate Available {}
}

// The camera

class CameraState extends Timeline
{
	predicate On {}
	predicate Off {}
	predicate poweringUp {}
	predicate poweringDown {}	
}

class Camera
{
    Nanosatellite nanosat;
	CameraState state;
	Window window;
	
	Camera(Nanosatellite n)
	{
		nanosat = n;
	    state = new CameraState();
	    window = new Window();
	}
		
	action takePhoto{
    	eq(1, duration);
	}
	
	action turnOn{
	    eq(2, duration);
	}
	
	action turnOff{
	    eq(2, duration);
	}
	
	action consumePowerWhileOn {
	    eq([0, 50], duration);
	}
}

// The solar panel

class SolarPanelState extends Timeline
{
	predicate Charging {}
}

class SolarPanel
{
    Nanosatellite nanosat;
	SolarPanelState state;
	Window window;
	
	Camera(Nanosatellite n)
	{
		nanosat = n;
	    state = new SolarPanelState();
	    window = new Window();
	}
	
	action chargeBattery {}
}

// The transmitter

class Transmitter
{
    Nanosatellite nanosat;
    Window window;
    
    Transmitter(Nanosatellite n)
    {
        nanosat = n;
        window = new Window();
    }
    
    action downlinkData{}
}

// Actions

Nanosatellite::takePhoto
{
	equals(object.camera.takePhoto);
}

Nanosatellite::chargeBattery
{
   equals(object.panel.chargeBattery);
}

Nanosatellite::downlinkData
{
    equals(object.transmitter.downlinkData);
}

Camera::takePhoto
{
    contained_by(condition object.state.On);
    contained_by(condition object.window.Available);
    contains(effect object.nanosat.memory.produce data);
    eq(data.quantity, 50);
}

Camera::turnOn
{
    contained_by(condition object.window.Available);
	met_by(condition object.state.Off);
	equals(effect object.state.poweringUp);
	meets(effect object.state.On);
}

Camera::turnOff
{
    contained_by(condition object.window.Available);
	met_by(condition object.state.On);
	equals(effect object.state.poweringDown);
	meets(effect object.state.Off);
}

Camera::consumePowerWhileOn
{
    contained_by(object.state.On);
    starts(effect object.nanosat.mainBattery.consume consumption);
    mulEq(duration, 12, consumption.quantity);
}

SolarPanel::chargeBattery
{
    contained_by(condition object.window.Available);
    equals(effect object.state.Charging);
    starts(effect object.nanosat.mainBattery.produce power);
  	eq(power.quantity, duration);
}

Transmitter::downlinkData
{
    after(object.nanosat.camera.takePhoto);
    contained_by(condition object.window.Available);
    contains(effect object.nanosat.memory.consume data);
    eq(data.quantity, duration);
}