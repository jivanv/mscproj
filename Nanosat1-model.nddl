class Nanosatellite;
class Battery;
class PayloadState;
class Window;
class SolarPanel;
class Memory;
class STX;
class Camera;
class C3D;

class Nanosatellite 
{
	Battery mainBattery;
	Memory memory;
	SolarPanel panel;
	STX stx;
	C3D c3d;
		    
	Nanosatellite(Battery b, Memory m)
	{
    	mainBattery = b;
    	memory = m;
        panel = new SolarPanel(this);
        stx = new STX(this);
        c3d = new C3D(this);
  	}
}

class C3D
{
    Nanosatellite nanosat;
    Camera camera;
    
    C3D(Nanosatellite n)
    {
        nanosat = n;
        camera = new Camera(this);
    }
}

class Camera
{
    Window window;
    PayloadState state;
    C3D c3d;
    
    Camera(C3D c)
    {
        c3d = c;
        window = new Window();
        state = new PayloadState();
    }
    
    action turnOnC3D {
	    eq(1, duration);
	}
	
	action turnOffC3D {
	    eq(1, duration);
	}
	
	action consumePowerWhileOnC3DCam {
	    eq(1, duration);
	}
	
	action runHouseKeepingMode {
	    eq(1, duration);
	}
	
	action takePhoto {
	    eq(30, duration);
	}
}

class Battery extends Reservoir
{
    string profileType;
    
    // ic = initial capacity, min, max
	Battery(float ic, float min, float max)
	{
		super(ic, min, max);
		profileType="IncrementalFlowProfile";
	}
}

class Memory extends Reservoir
{
    string profileType;
    
    // ic = initial capacity, min, max
	Memory(float ic, float min, float max)
	{
		super(ic, min, max);
		profileType="IncrementalFlowProfile";
	}
}

// Time window for availability
class Window extends Timeline
{
    predicate Available {}
}

class PayloadState extends Timeline
{
	predicate On {}
	predicate Off {}
	predicate poweringUp {}
	predicate poweringDown {}	
}

class SolarPanel
{
    Nanosatellite nanosat;
    Window window;
	
	SolarPanel(Nanosatellite n)
	{
		nanosat = n;
		window = new Window();
	}
	
	action chargeBattery {
	    eq(1, duration);
	}
}

class STX
{
    Nanosatellite nanosat;
    Window window;
    
    STX(Nanosatellite n)
    {
        nanosat = n;
        window = new Window();
    }
    
    action downlinkData{
        eq(1, duration);
    }
}


// Actions

Camera::takePhoto
{
    contained_by(object.window.Available);
    contained_by(condition object.state.On);
    met_by(object.runHouseKeepingMode);
    meets(effect object.turnOffC3D);
    starts(effect object.c3d.nanosat.memory.produce data);
    eq(data.quantity, 691200);
}

Camera::turnOnC3D
{
    contained_by(object.window.Available);
    met_by(condition object.state.Off);
	equals(effect object.state.poweringUp);
	meets(effect object.state.On);
	meets(object.runHouseKeepingMode);
}

Camera::turnOffC3D
{
    contained_by(object.window.Available);
	met_by(condition object.state.On);
	equals(effect object.state.poweringDown);
	meets(effect object.state.Off);
	met_by(object.takePhoto);
}

Camera::runHouseKeepingMode
{
    contained_by(object.window.Available);
    contained_by(condition object.state.On);
    met_by(object.turnOnC3D);
    meets(object.takePhoto);
    starts(effect object.c3d.nanosat.memory.produce data);
    eq(data.quantity, 256);
    starts(effect object.c3d.nanosat.mainBattery.consume power);
    eq(power.quantity, 13);
}

Camera::consumePowerWhileOnC3DCam
{
    contained_by(condition object.window.Available);
    contained_by(condition object.state.On);
    bool continueLoopC;
    if(continueLoopC) {
        meets(effect object.consumePowerWhileOnC3DCam next);
        next.end <= 1500;
        starts(effect object.c3d.nanosat.mainBattery.consume p);
        eq(p.quantity, 13);
    }
}

SolarPanel::chargeBattery
{
    contained_by(condition object.window.Available);
  	bool continueLoopB;
    if(continueLoopB) {
        meets(effect object.chargeBattery next);
        next.end <= 1500;
        contains(effect object.nanosat.mainBattery.produce power);
        eq(power.quantity, 9);
    }
}

STX::downlinkData
{
    contained_by(condition object.window.Available);
    bool continueLoopS;
    if(continueLoopS) {
        meets(effect object.downlinkData next);
        next.end <= 1500;
        starts(effect object.nanosat.memory.consume data);
        eq(data.quantity, 78643);
        starts(effect object.nanosat.mainBattery.consume consumption);
        eq(consumption.quantity, 453);
    }
}