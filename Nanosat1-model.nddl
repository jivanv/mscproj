class Nanosatellite;
class Battery;
class PayloadState;
class Window;
class Memory;
class C3D;
class SolarPanel;
class TOPCAT;
class JANUS;
class MIC;
class UVTRX;
class ConstantlyActivePayload;
class FUNCUBE;
class STX;

class Nanosatellite 
{
	Battery mainBattery;
	Memory memory;
	C3D c3d;
	SolarPanel panel;
	TOPCAT topcat;
	JANUS janus;
	MIC mic;
	UVTRX uvtrx;
	ConstantlyActivePayload constantpayloads;
	FUNCUBE funcube2;
	STX stx;
		    
	Nanosatellite(Battery b, Memory m)
	{
    	mainBattery = b;
    	memory = m;
        c3d = new C3D(this);
        panel = new SolarPanel(this);
        topcat = new TOPCAT(this);
        janus = new JANUS(this);
        mic = new MIC(this);
        uvtrx = new UVTRX(this);
        constantpayloads = new ConstantlyActivePayload(this);
        funcube2 = new FUNCUBE(this);
        stx = new STX(this);
  	}
}

class C3D extends Timeline
{
    Nanosatellite nanosat;
    PayloadState state;
    Window camWindow;
    Window rmWindow;
    
    C3D(Nanosatellite n)
    {
        nanosat = n;
        state = new PayloadState();
        camWindow = new Window();
        rmWindow = new Window();
    }
    
    action turnOnC3D {
	    eq(1, duration);
	}
	
	action turnOffC3D {
	    eq(1, duration);
	}
    
    action takePhoto {
	    eq(30, duration);
	}
	
	action runHouseKeepingModeBeforeCamera {
	    eq(1, duration);
	}
	
	action runHouseKeepingModeBeforeRM {
	    eq(1, duration);
	}
	
	action measureRadiation {
	    eq(40, duration);
	}
}

class Battery extends Reservoir
{
    string profileType;
    
    // initial capacity, min, max, max production rate, max production overall, max consumption rate, max consumption overall
	Battery(float ic, float min, float max)
	{
		super(ic, min, max);
		profileType="IncrementalFlowProfile";
	}
}

class Memory extends Reservoir
{
    string profileType;
    
    // ic = initial capacity, min, max
	Memory(float ic, float min, float max)
	{
		super(ic, min, max);
		profileType="IncrementalFlowProfile";
	}
}

// Time window for availability
class Window extends Timeline
{
    predicate Available {}
}

class PayloadState extends Timeline
{
	predicate On {}
	predicate Off {}
	predicate poweringUp {}
	predicate poweringDown {}	
}

class SolarPanel
{
    Nanosatellite nanosat;
    Window window;
	
	SolarPanel(Nanosatellite n)
	{
		nanosat = n;
		window = new Window();
	}
	
	action chargeBattery {
	    eq([46,50], duration);
	}
}

class TOPCAT
{
    Nanosatellite nanosat;
    PayloadState state;
    Window window;
    
    TOPCAT(Nanosatellite n)
    {
        nanosat = n;
        state = new PayloadState();
        window = new Window();
    }
    
    action turnOnTOPCAT{
	    eq(15, duration);
	}
	
	action turnOffTOPCAT{
	    eq(1, duration);
	}
	
	action runExperimentTOPCAT {
	    eq(40, duration);
	}
}

class JANUS extends Timeline
{
    Nanosatellite nanosat;
    Window window;
    PayloadState state;
    
    JANUS(Nanosatellite n)
    {
        nanosat = n;
        window = new Window();
        state = new PayloadState();
    }
    
    action runExperimentLowPowerJANUS {
        eq(35, duration);
    }
    
    action runExperimentHighPowerJANUS {
        eq(20, duration);
    }
}

class MIC
{
    Nanosatellite nanosat;
    PayloadState state;
    Window window;
    
    MIC(Nanosatellite n)
    {
        nanosat = n;
        state = new PayloadState();
        window = new Window();
    }
    
    action turnOnMIC{
	    eq(1, duration);
	}
	
	action turnOffMIC{
	    eq(1, duration);
	}
	
	action runExperimentMIC {
	    eq(15, duration);
	}
}

class UVTRX extends Timeline
{
    Nanosatellite nanosat;
    Window beaconWindow;
    Window downlinkWindow;
    
    UVTRX(Nanosatellite n)
    {
        nanosat = n;
        downlinkWindow = new Window();
        beaconWindow = new Window();
    }
    
    action downlinkData{
        eq([10,12], duration);
    }
    
    action beaconSignal{
        eq([1,685], duration);
    }

}

class ConstantlyActivePayload
{
    Nanosatellite nanosat;
    float consumptionRate;
    
    ConstantlyActivePayload(Nanosatellite n)
    {
        nanosat = n;
    }
    
    action alwaysOn {
        eq(1440, duration);
    }
}

class FUNCUBE extends Timeline
{
    Nanosatellite nanosat;
    Window educationWindow;
    Window amateurWindow;
    
    FUNCUBE(Nanosatellite n)
    {
        nanosat = n;
        educationWindow = new Window();
        amateurWindow = new Window();
    }
    
    action educationalCommunicationsFUNCUBE {
        eq([46,50], duration);
    }
    
    action amateurCommunicationsFUNCUBE {
        eq([29,49], duration);
    }
}

class STX
{
    Nanosatellite nanosat;
    Window window;
    
    STX(Nanosatellite n)
    {
        nanosat = n;
        window = new Window();
    }
    
    action downlinkData{
        eq([6,13], duration);
    }
}

// Actions

C3D::turnOnC3D
{
    contained_by(condition object.nanosat.mic.state.Off);
    met_by(condition object.state.Off);
	equals(effect object.state.poweringUp);
	meets(effect object.state.On);
	meets(condition object.runHouseKeepingModeBeforeRM);
}

C3D::turnOffC3D
{
	met_by(condition object.state.On);
	equals(effect object.state.poweringDown);
	meets(effect object.state.Off);
}

C3D::takePhoto
{
    starts_during(condition object.nanosat.uvtrx.downlinkWindow.Available);
    contained_by(condition object.camWindow.Available);
    contained_by(condition object.state.On);
    met_by(object.runHouseKeepingModeBeforeCamera);
    ends(effect object.nanosat.memory.produce data);
    eq(data.quantity, 691200);
    starts(effect object.nanosat.mainBattery.consume p);
    mulEq(duration, 54, p.quantity);
}

C3D::runHouseKeepingModeBeforeCamera
{
    contained_by(condition object.state.On);
    meets(object.takePhoto);
    ends(effect object.nanosat.memory.produce data);
    eq(data.quantity, 256);
    starts(effect object.nanosat.mainBattery.consume power);
    eq(power.quantity, 54);
}

C3D::measureRadiation
{
    starts_during(condition object.rmWindow.Available);
    contained_by(condition object.state.On);
    met_by(object.runHouseKeepingModeBeforeRM);
    ends(effect object.nanosat.memory.produce data);
    eq(data.quantity, 18432);
    starts(effect object.nanosat.mainBattery.consume p);
    mulEq(duration, 54, p.quantity);
}

C3D::runHouseKeepingModeBeforeRM
{
    contained_by(condition object.state.On);
    meets(object.measureRadiation);
    ends(effect object.nanosat.memory.produce data);
    eq(data.quantity, 256);
    starts(effect object.nanosat.mainBattery.consume power);
    eq(power.quantity, 54);
}

SolarPanel::chargeBattery
{
    contained_by(condition object.window.Available);
  	starts(effect object.nanosat.mainBattery.produce power);
    mulEq(duration, [0,390], power.quantity);
}

TOPCAT::turnOnTOPCAT
{
    contained_by(condition object.window.Available);
	met_by(condition object.state.Off);
	equals(effect object.state.poweringUp);
	meets(effect object.state.On);
}

TOPCAT::turnOffTOPCAT
{
    contained_by(condition object.window.Available);
	met_by(condition object.state.On);
	equals(effect object.state.poweringDown);
	meets(effect object.state.Off);
}

TOPCAT::runExperimentTOPCAT
{
    contained_by(condition object.window.Available);
    met_by(object.turnOnTOPCAT);
    meets(object.turnOffTOPCAT);
    contained_by(condition object.state.On);
    ends(effect object.nanosat.memory.produce data);
    eq(data.quantity, 204800);
    starts(effect object.nanosat.mainBattery.consume power);
    mulEq(duration, 96, power.quantity);
    
}

JANUS::runExperimentLowPowerJANUS
{
    ends(effect object.nanosat.memory.produce data);
    eq(data.quantity, 512);
    starts(effect object.nanosat.mainBattery.consume power);
    mulEq(duration, 46.8, power.quantity);
}

JANUS::runExperimentHighPowerJANUS
{
    contained_by(condition object.nanosat.topcat.state.Off);
    contained_by(condition object.nanosat.c3d.state.Off);
    contained_by(condition object.nanosat.mic.state.Off);
    starts_during(condition object.window.Available);
    ends(effect object.nanosat.memory.produce data);
    eq(data.quantity, 512);
    starts(effect object.nanosat.mainBattery.consume power);
    mulEq(duration, 144, power.quantity);
}

MIC::turnOnMIC
{
    contained_by(condition object.window.Available);
    contained_by(condition object.nanosat.c3d.state.Off);
	met_by(condition object.state.Off);
	equals(effect object.state.poweringUp);
	meets(effect object.state.On);
}

MIC::turnOffMIC
{
    contained_by(condition object.window.Available);
	met_by(condition object.state.On);
	equals(effect object.state.poweringDown);
	meets(effect object.state.Off);
}

MIC::runExperimentMIC
{
    contained_by(condition object.window.Available);
    contained_by(condition object.nanosat.c3d.state.Off);
    contained_by(condition object.nanosat.topcat.state.Off);
    met_by(object.turnOnMIC);
    meets(object.turnOffMIC);
    contained_by(condition object.state.On);
    ends(effect object.nanosat.memory.produce data);
    eq(data.quantity, 34816);
    starts(effect object.nanosat.mainBattery.consume power);
    mulEq(duration, 60, power.quantity);
}

UVTRX::downlinkData
{
    contained_by(condition object.downlinkWindow.Available);
    starts(effect object.nanosat.memory.consume data);
    mulEq(duration, [0,72000], data.quantity);
    starts(effect object.nanosat.mainBattery.consume power);
    mulEq(duration, 23.4, power.quantity);
}

UVTRX::beaconSignal
{
    starts(effect object.nanosat.mainBattery.consume power);
    mulEq(duration, 17.7, power.quantity);
}

ConstantlyActivePayload::alwaysOn
{
    starts(effect object.nanosat.mainBattery.consume power);
    mulEq(duration, 53.4, power.quantity);
}

FUNCUBE::educationalCommunicationsFUNCUBE
{
    contained_by(condition object.educationWindow.Available);
    starts(effect object.nanosat.mainBattery.consume power);
    mulEq(duration, 60.84, power.quantity);
}

FUNCUBE::amateurCommunicationsFUNCUBE
{
    contained_by(condition object.amateurWindow.Available);
    starts(effect object.nanosat.mainBattery.consume power);
    mulEq(duration, 58.2, power.quantity);
}

STX::downlinkData
{
    contained_by(condition object.window.Available);
    starts(effect object.nanosat.memory.consume data);
    mulEq(duration, [0,4194304], data.quantity);
    starts(effect object.nanosat.mainBattery.consume power);
    mulEq(duration, 163.2, power.quantity);
}